apply plugin: 'java'
apply plugin: 'maven'

repositories {
    mavenCentral()
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}


dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.4'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3.RC2'
//    integrationTestCompile group: 'org.assertj', name: 'assertj-core', version: '3.0.0'
//    'org.assertj:assertj-core:3.0.0'
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test


tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}